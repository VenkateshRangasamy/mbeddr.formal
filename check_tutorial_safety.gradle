import de.itemis.mps.gradle.*

buildscript {
    repositories {
        maven { url 'https://projects.itemis.de/nexus/content/repositories/mbeddr' }
    }
    dependencies {
        classpath 'de.itemis.mps:mps-gradle-plugin:1.4+'
    }
}

plugins {
    id 'base'
    id 'maven-publish'
}

apply plugin: 'modelcheck'

// Dependency versions
ext.mpsMajor =  '2020.2'
ext.mpsVersion =  "$mpsMajor.+"

ext.dependencyRepositories = [
        'https://projects.itemis.de/nexus/content/repositories/mbeddr',
        'https://projects.itemis.de/nexus/content/repositories/mbeddr_snapshots',
]

configurations {
    mps
}

dependencies {
    mps "com.jetbrains:mps:$mpsVersion"
}

repositories {
    mavenLocal()
    for (repoUrl in project.dependencyRepositories) {
        maven {
            url repoUrl
        }
    }
    mavenCentral()
}


File modelcheckresult = new File("$buildDir/TEST-tutorial-safety-modelcheckresult.xml")

def pluginsList=[]
new File("$buildDir/dependencies/com.mbeddr.platform").eachFile {
    if(it.isDirectory()) {
        pluginsList.add(new Plugin(it.getName(), it.absolutePath))
    }
} 
new File("$buildDir/artifacts/com.mbeddr.formal.languages").eachFile {
    if(it.isDirectory()) {
        pluginsList.add(new Plugin(it.getName(), it.absolutePath))
    }
} 

modelcheck {
	mpsLocation = new File("$buildDir/mps")
    macros = [new Macro("mbeddr.formal.home", "$projectDir")]
	plugins = pluginsList
    projectLocation = new File("$projectDir/code/tutorial-safety/")
    mpsConfig = configurations.mps
    junitFile = modelcheckresult
    errorNoFail = true
    maxHeap = "3G"
    modules = ["com.mbeddr.formal.safety.tutorial"] //, "com.mbeddr.formal.safety.users_guide"]
}

defaultTasks 'checkmodels'